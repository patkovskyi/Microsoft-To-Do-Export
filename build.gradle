plugins {
    id 'java'
    id 'application'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.8'
}

version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDir('src')
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

run {
    standardInput = System.in
}

application {
    mainClass = 'Main'
}

jar {
    manifest {
        attributes("Main-Class": "Main")
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}

idea.project.settings.runConfigurations {
    'Main'(org.jetbrains.gradle.ext.Application) {
        mainClass = 'Main'
        moduleName = project.idea.module.name + '.main'
        jvmArgs = '-Xmx256m'
    }
}

dependencies {
    implementation 'org.json:json:20240303'
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'
    implementation 'org.apache.httpcomponents:httpcore:4.4.16'
}
